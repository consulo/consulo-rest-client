{
  parserClass="org.javamaster.httpclient.parser.HttpParser"
  parserUtilClass="org.javamaster.httpclient.parser.HttpParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Http"
  psiImplClassSuffix="Impl"
  psiPackage="org.javamaster.httpclient.psi"
  psiImplPackage="org.javamaster.httpclient.psi.impl"

  elementTypeHolderClass="org.javamaster.httpclient.psi.HttpTypes"
  elementTypeClass="org.javamaster.httpclient.psi.HttpElementType"
  tokenTypeClass="org.javamaster.httpclient.psi.HttpTokenType"
  psiImplUtilClass="org.javamaster.httpclient.psi.impl.HttpPsiImplUtil"

  tokens=[
    LINE_COMMENT='regexp://.*'
    REQUEST_COMMENT='regexp:###.*'

    HTTP='http'
    HTTPS='https'
    SCHEMA_SEPARATE='://'
    COLON=':'

    SLASH='/'
    QUESTION='?'
    EQUALS='='
    AND='&'
    HASH='#'
    DOLLAR='$'

    POST='POST'
    GET='GET'
    WEBSOCKET='WEBSOCKET'
    DUBBO='DUBBO'
    DIFFERENCE_FILE='DIFFERENCE_FILE'
    MESSAGE_SEPARATOR='MESSAGE_SEPARATOR'
    MESSAGE_TEXT='MESSAGE_TEXT'
    DYNAMIC_SIGN='DYNAMIC_SIGN'
    SCHEME_SEPARATOR='SCHEME_SEPARATOR'
    IDENTIFIER='IDENTIFIER'
    INPUT_SIGN='< '
    OUTPUT_FILE_SIGN='>> '
    GLOBAL_START_SCRIPT_BRACE='<! {%'
    IN_START_SCRIPT_BRACE='< {%'
    OUT_START_SCRIPT_BRACE='> {%'
    END_SCRIPT_BRACE='%}'
    SRTART_VARIABLE_BRACE='{{'
    END_VARIABLE_BRACE='}}'
  ]
}

httpFile ::= globalHandler? request_block*

globalHandler ::= globalScript
globalScript ::= GLOBAL_START_SCRIPT_BRACE scriptBody END_SCRIPT_BRACE {
    pin = 1
}
request_block ::=  comment+ preRequestHandler? request
comment ::= REQUEST_COMMENT
preRequestHandler ::= preRequestScript
preRequestScript ::= IN_START_SCRIPT_BRACE scriptBody END_SCRIPT_BRACE {
    pin = 1
}

request ::= method requestTarget headerField* requestMessagesGroup? multipartMessage? responseHandler? outputFile? {
    methods = [
        getContentType
        getContentTypeBoundary
        getContentLength
        getHttpVersion
        getHttpHost
    ]
    pin = 1
}

method ::= REQUEST_METHOD {
    mixin = "org.javamaster.httpclient.psi.impl.HttpMethodBase"
}

requestTarget ::= (variable | schema SCHEMA_SEPARATE) host? port? pathAbsolute? (QUESTION query)? (HASH fragment)? version? {
    methods = [
        getHttpUrl
        getReferences
    ]
}
schema ::= SCHEMA_PART
host ::= HOST_VALUE | variable

variable ::= SRTART_VARIABLE_BRACE DOLLAR? IDENTIFIER END_VARIABLE_BRACE {
    methods = [
        getName
        getReferences
    ]
    pin = 1
}
dynamicVariable ::=

port ::= COLON PORT_SEGMENT
pathAbsolute ::= (SLASH (SEGMENT | variable))+
query ::= queryParameter (AND queryParameter)*
queryParameter ::= queryParameterKey EQUALS? queryParameterValue? {
    pin = 1
}
queryParameterKey ::= QUERY_NAME | variable
queryParameterValue ::= QUERY_VALUE | variable
fragment ::= FRAGMENT_PART
version ::= HTTP_VERSION

headerField ::= headerFieldName COLON headerFieldValue? {
    methods = [
        getName
        getValue
    ]
    pin = 1
}
headerFieldName ::= FIELD_NAME
headerFieldValue ::= (FIELD_VALUE | variable | FIELD_VALUE variable | variable FIELD_VALUE)+ {
    methods = [
        getReferences
    ]
}

requestMessagesGroup ::= inputFile | messageBody

inputFile ::= INPUT_SIGN filePath {
    pin = 1
}
filePath ::= INPUT_FILE_PATH_PART {
    methods = [
        getReferences
    ]
}
messageBody ::= exMessageBody {
    mixin="org.javamaster.httpclient.inject.HttpPsiLanguageInjectionHost"
}
external exMessageBody ::= message_text

multipartMessage ::= (MESSAGE_BOUNDARY multipartField?)+
multipartField ::= headerField+ requestMessagesGroup {
    methods = [
        getContentType
    ]
}

responseHandler ::= responseScript {
    methods = [
        getResponseScriptHolder
    ]
}
responseScript ::= OUT_START_SCRIPT_BRACE scriptBody END_SCRIPT_BRACE {
    pin = 1
}
scriptBody ::= exScriptBody {
  mixin="org.javamaster.httpclient.inject.HttpPsiLanguageInjectionHost"
}
external exScriptBody ::= script_body

outputFile ::= OUTPUT_FILE_SIGN outputFilePath
outputFilePath ::= OUTPUT_FILE_PATH_PART {
    methods = [
        getReferences
    ]
    implements = "org.javamaster.httpclient.psi.HttpFilePath"
}